plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

def mcVer = project.findProperty("mcVer")
def versionProps = new Properties()

if (mcVer != null) {
    def versionPropsFile = file("versions/${mcVer}/gradle.properties")
    if (versionPropsFile.exists()) {
        versionPropsFile.withInputStream { versionProps.load(it) }
    } else {
        throw new GradleException("versions/${mcVer}/gradle.properties not found!")
    }
} else {
    def rootPropsFile = file("gradle.properties")
    if (rootPropsFile.exists()) {
        rootPropsFile.withInputStream { versionProps.load(it) }
    } else {
        throw new GradleException("Root gradle.properties not found!")
    }
}

versionProps.each { key, value -> project.ext[key.toString()] = value.toString() }

version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://maven.parchmentmc.org" } // Parchment repo
    maven { url = "https://maven.createmod.net" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://mvn.devos.one/releases" } // Porting Lib releases
    maven { url = "https://mvn.devos.one/snapshots" } // Create and several dependencies
    maven { url = "https://maven.tterrag.com/" } // Flywheel
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven" } // Forge Config API Port
    maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
}

dependencies {
    minecraft "com.mojang:minecraft:${project.ext.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.ext.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.ext.fabric_version}"
    modImplementation "com.simibubi.create:create-fabric-1.20.1:${create_fabric_version}"
}

processResources {
    inputs.property("version", project.version)
    inputs.property("minecraft_version", project.ext.minecraft_version)
    inputs.property("loader_version", project.ext.loader_version)

    filesMatching("fabric.mod.json") {
        expand(version: project.version,
                minecraft_version: project.ext.minecraft_version,
                loader_version: project.ext.loader_version)
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 25
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

jar {
    archiveFileName = "${project.archives_base_name}-fabric-${project.mod_version}+mc${project.ext.minecraft_version}-dev.jar"
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

remapJar {
    archiveFileName = "${project.archives_base_name}-fabric-${project.mod_version}+mc${project.ext.minecraft_version}.jar"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }
}